import com.wiredforcode.gradle.spawn.KillProcessTask
import com.wiredforcode.gradle.spawn.SpawnProcessTask
import org.apache.tools.ant.filters.*

buildscript {
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/libs-release" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.1.5.RELEASE"
        classpath 'com.wiredforcode:gradle-spawn-plugin:0.5.2'
        classpath 'org.cloudfoundry:cf-gradle-plugin:1.0.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'spawn'
apply plugin: 'cloudfoundry'

repositories {
    jcenter()
    maven { url 'http://repo.spring.io/release' }
}

dependencies {
    compile "org.codehaus.groovy:groovy:2.3.6"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-remote-shell"
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile("org.springframework.social:spring-social-twitter")
    compile("org.springframework.security.oauth:spring-security-oauth2:2.0.1.RELEASE")
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'junit:junit:4.11'
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'info.cukes:cucumber-junit:1.1.5'
    testCompile 'info.cukes:cucumber-groovy:1.1.5'
    testCompile "org.mongodb:mongo-java-driver:2.9.1"
    testCompile 'com.github.groovy-wslite:groovy-wslite:0.8.0'
}

test.testLogging.exceptionFormat = 'full'

configurations {
    functTestCompile.extendsFrom testCompile
    functTestRuntime.extendsFrom testRuntime
}

sourceSets {
    functionalTest {
        groovy.srcDir file('src/testFunctional/groovy')
        resources.srcDir     file('src/testFunctional/resources')
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.functTestCompile
        runtimeClasspath = output + compileClasspath + configurations.functTestRuntime
    }
}

processResources {
    filter ReplaceTokens, tokens: ["application.version": project.version]
}

task startServer(type: SpawnProcessTask, dependsOn: 'assemble') {
    command "java -jar ${projectDir}/build/libs/${project.name}-${project.version}.jar"
    ready 'Started Application'
}

task stopServer(type: KillProcessTask)

task functionalTest(type: Test) {
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
    dependsOn startServer
    finalizedBy stopServer
}

check.dependsOn functionalTest

cloudfoundry {
    file = file("${projectDir}/build/libs/${project.name}-${project.version}.jar")
    buildpack = "https://github.com/marcoVermeulen/java-buildpack"
    target = 'https://api.run.pivotal.io'
    organization = envVariableAsGlobal("ORG", true)
    username = envVariableAsGlobal("USERNAME", true)
    password = envVariableAsGlobal("PASSWORD", true)
    space = envVariableAsGlobal("SPACE", false)
    application = envVariableAsGlobal("APP", false)
    domain = envVariableAsGlobal("DOMAIN", true)
    host = envVariableAsGlobal("HOST", false)
    uri = envVariableAsGlobal("URI", false)
    env = [
        MONGO_HOST: envVariableAsGlobal("MONGO_HOST", false),
        MONGO_PORT: envVariableAsGlobal("MONGO_PORT", false),
        MONGO_DB_NAME: envVariableAsGlobal("MONGO_DB_NAME", false),
        MONGO_USERNAME: envVariableAsGlobal("MONGO_USERNAME", false),
        MONGO_PASSWORD: envVariableAsGlobal("MONGO_PASSWORD", false),
        CLIENT_ID: envVariableAsGlobal("CLIENT_ID", true),
        CLIENT_SECRET: envVariableAsGlobal("CLIENT_SECRET", true),
        AUTH_USERNAME: envVariableAsGlobal("AUTH_USERNAME", true),
        AUTH_PASSWORD: envVariableAsGlobal("AUTH_PASSWORD", true)
    ]
}

def envVariableAsGlobal(String envVar, boolean global) {
    def env = global ? "" : determineEnvironment()
    System.getenv("${envVar}${env}") ?: 'invalid'
}

def determineEnvironment() {
    hasProperty("env") ? "_${env.toUpperCase()}" : '_LOCAL'
}
